plugins {
	id 'java'
	id 'application'
	id 'jvm-test-suite'
	id 'org.springframework.boot' version '4.0.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'maven-publish'
	id 'jacoco'
}

group = 'com.fujitsu.edgewareroad'

def appProps = new Properties()
file("src/main/resources/application.properties").withInputStream { appProps.load(it) }

version = appProps.getProperty("trivysummary.version")

def javaHome = System.properties['java.home']

repositories {
	mavenCentral()
}

sourceSets {
        main {
                java {
                        srcDirs= ["src/main/java"]
                }
                resources {
                        srcDirs= ["src/main/resources"]
                }
        }
        test {
                java {
                        srcDirs= ["src/test/java"]
                }
                resources {
                        srcDirs= ["src/test/resources"]
                }
        }
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
	sourceCompatibility = '25'
	targetCompatibility = '25'
}

compileJava {
	// options.compilerArgs << '--add-exports=java.desktop/sun.awt=ALL-UNNAMED'
}

tasks.withType(JavaCompile).configureEach {
	// Needed for IDE builds which fail as Lombok cannot access sun.misc.Unsafe
	options.forkOptions.jvmArgs << '--sun-misc-unsafe-memory-access=allow'
}

application {
	mainClass = 'com.fujitsu.edgewareroad.trivysummary.TrivySummaryApp'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-json'
	implementation 'org.thymeleaf:thymeleaf:3.1.3.RELEASE'
	implementation 'io.github.openhtmltopdf:openhtmltopdf-pdfbox:1.1.31'
	implementation 'io.github.openhtmltopdf:openhtmltopdf-slf4j:1.1.31'
	implementation ('io.github.openhtmltopdf:openhtmltopdf-svg-support:1.1.31') {
		exclude group: 'commons-io', module: 'commons-io'
	}
	implementation 'commons-io:commons-io:2.21.0'
	implementation 'io.github.openhtmltopdf:openhtmltopdf-core:1.1.31'
	implementation 'org.jsoup:jsoup:1.21.2'
	implementation 'org.apache.xmlgraphics:batik-svggen:1.19'
	implementation 'org.apache.xmlgraphics:batik-dom:1.19'
	implementation 'org.apache.commons:commons-lang3:3.19.0'
	compileOnly 'org.projectlombok:lombok:1.18.42'
	annotationProcessor 'org.projectlombok:lombok:1.18.42'
	testCompileOnly 'org.projectlombok:lombok:1.18.42'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	manifest {
		attributes(
			'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
			'Main-Class': application.mainClass,
			'Implementation-Version': version
		)
	}

	from(configurations.runtimeClasspath.files.collect { it.isDirectory() ? it : zipTree(it) }) {}
}

//create a Jar of the sources
task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
    archiveClassifier = "sources"
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

// Publishing the output to GitHub (standard JAR, sources plus POM file, distribution ZIP)
publishing {
	publications {
		maven(MavenPublication) {
			groupId = 'com.fujitsu.edgewareroad'
			artifacts = [ jar, sourcesJar, distZip ]
			pom {
				name = 'trivysummary'
				description = 'Reporting app to summarise Trivy JSON output, either for single scans or to compare two scans. Also useful in CI/CD pipelines to auto-fail if vulnerabilities above a stated severity are in a component'
			}
			pom.withXml {
			    def dependenciesNode = asNode().appendNode('dependencies')
			    configurations.implementation.allDependencies.each {
			        def dependencyNode = dependenciesNode.appendNode('dependency')
			        dependencyNode.appendNode('groupId', it.group)
			        dependencyNode.appendNode('artifactId', it.name)
			        dependencyNode.appendNode('version', it.version)
			    }
			}
		}
	}
	repositories {
		maven {
			name = 'GitHubPackages'
			credentials {
				username = project.findProperty("mvnpub.user") ?: System.getenv("USERNAME")
				password = project.findProperty("mvnpub.key") ?: System.getenv("PASSWORD")
			}
			url = uri('https://maven.pkg.github.com/EdgewareRoad/TrivySummary')
		}
	}
}

