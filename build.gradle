plugins {
	id 'java'
	id 'application'
	id 'jvm-test-suite'
	id 'org.springframework.boot' version '3.2.2'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.fujitsu.edgewareroad'
version = '0.0.1-SNAPSHOT'

repositories {
	mavenCentral()
}


java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	sourceCompatibility = '21'
}

application {
	mainClass = 'com.fujitsu.edgewareroad.trivysummary.TrivySummaryApp'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
	implementation 'com.fasterxml.jackson.core:jackson-annotations:2.16.1'
	implementation 'org.thymeleaf:thymeleaf:3.1.2.RELEASE'
	implementation 'org.jsoup:jsoup:1.17.2'
	implementation 'com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10'
	implementation 'com.openhtmltopdf:openhtmltopdf-slf4j:1.0.10'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	manifest {
		attributes(
			'Class-Path': configurations.runtimeClasspath.files.collect { it.getName() }.join(' '),
			'Main-Class': application.mainClass,
			'Implementation-Version': version
		)
	}

	from(configurations.runtimeClasspath.files.collect { it.isDirectory() ? it : zipTree(it) }) {}
}

testing {
	suites {
		test {
			useJUnitJupiter()
		}
	}
}
